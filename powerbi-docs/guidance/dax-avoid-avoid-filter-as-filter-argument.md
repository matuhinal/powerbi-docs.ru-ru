---
title: 'DAX: Предотвращение использования FILTER в качестве аргумента фильтра'
description: Рекомендации по использованию функции FILTER в качестве аргумента фильтра.
author: peter-myers
ms.reviewer: asaxton
ms.service: powerbi
ms.subservice: powerbi-desktop
ms.topic: conceptual
ms.date: 12/30/2019
ms.author: v-pemyer
ms.openlocfilehash: 5120b1ec0ce1acbe746dabe2b11b5653a34a4603
ms.sourcegitcommit: 2131f7b075390c12659c76df94a8108226db084c
ms.translationtype: HT
ms.contentlocale: ru-RU
ms.lasthandoff: 08/03/2020
ms.locfileid: "87537603"
---
# <a name="dax-avoid-using-filter-as-a-filter-argument"></a>DAX: Предотвращение использования FILTER в качестве аргумента фильтра

При моделировании данных можно создавать выражения DAX, которые необходимо вычислить в измененном контексте фильтра. Например, можно создать определение меры, чтобы вычислить продажи для продуктов с высоким уровнем прибыли. Такое вычисление описано далее в этой статье.

> [!NOTE]
> Сведения в данной статье особенно важны для вычислений модели, которые применяют фильтры для импорта таблиц.

Функции [CALCULATE](/dax/calculate-function-dax) и [CALCULATETABLE](/dax/calculatetable-function-dax) языка DAX являются важными и полезными функциями. С их помощью можно создавать вычисления, которые удаляют или добавляют фильтры или изменяют пути связей. Это выполняется путем передачи аргументов фильтра, которые являются логическими выражениями, табличными выражениями или специальными функциями фильтрации. В этой статье рассматриваются только логические и табличные выражения.

Рассмотрим следующее определение меры, которое вычисляет продажи красного продукта с помощью табличного выражения. Оно заменит все фильтры, которые могут быть применены к таблице **Product**.

```dax
Red Sales =
CALCULATE(
    [Sales],
    FILTER('Product', 'Product'[Color] = "Red")
)
```

Функция CALCULATE принимает табличное выражение, возвращаемое функцией DAX [FILTER](/dax/filter-function-dax), которая вычисляет выражение фильтра для каждой строки таблицы **Product**. Это обеспечивает правильный результат — результат продаж красных продуктов. Однако это можно сделать гораздо более эффективно, используя логическое выражение.

Ниже приведено усовершенствованное определение меры, в котором вместо табличного выражения используется логическое выражение. Функция DAX [KEEPFILTERS](/dax/keepfilters-function-dax) гарантирует, что все существующие фильтры, примененные к столбцу **Color**, сохраняются и не перезаписываются.

```dax
Red Sales =
CALCULATE(
    [Sales],
    KEEPFILTERS('Product'[Color] = "Red")
)
```

По возможности рекомендуется передавать аргументы фильтра как логические выражения. Это обусловлено тем, что таблицы импорта модели представляют собой хранилища столбцов в памяти. Они были явно оптимизированы для эффективной фильтрации столбцов таким способом.

Однако существуют ограничения, применяемые к логическим выражениям, когда они используются в качестве аргументов фильтра. А именно:

- они не позволяют сравнить столбцы с другими столбцами;
- они не позволяют сослаться на меру;
- в них нельзя использовать вложенные функции CALCULATE;
- они не позволяют использовать функции, которые проверяют или возвращают таблицу.

Это означает, что при наличии более сложных требований к фильтрам необходимо использовать табличные выражения.

Теперь рассмотрим другое определение меры.

```dax
High Margin Product Sales =
CALCULATE(
    [Sales],
    FILTER(
        'Product',
        'Product'[ListPrice] > 'Product'[StandardCost] * 2
    )
)
```

Определение _продукта с высоким уровнем прибыли_ — это продукт, у которого цена по прейскуранту в два раза превышает стандартную стоимость. В этом примере необходимо использовать функцию FILTER. Это обусловлено тем, что критерий фильтра слишком сложен для логического выражения.

Вот еще один пример. На этот раз требуется вычислить продажи, но только для месяцев, в которые была получена прибыль.

```dax
Sales for Profitable Months =
CALCULATE(
    [Sales],
    FILTER(
        VALUES('Date'[Month]),
        [Profit] > 0)
    )
)
```

В этом примере также необходимо использовать функцию FILTER. Это обусловлено тем, что требуется оценка меры **Profit**, чтобы исключить те месяцы, в которых не удалось получить прибыль. Нельзя использовать меру в логическом выражении, когда она используется в качестве аргумента фильтра.

## <a name="recommendations"></a>Рекомендации

Для достижения наилучших результатов мы рекомендуем использовать логические выражения как аргументы фильтра, когда это возможно.

Поэтому функцию FILTER следует использовать только по необходимости. Ее можно использовать для выполнения фильтрации сложных сравнений столбцов. Такие сравнения столбцов могут включать следующее.

- Меры
- Другие столбцы
- Использование функции [OR](/dax/or-function-dax) языка DAX или логического оператора OR (||)

## <a name="next-steps"></a>Дальнейшие действия

Дополнительные сведения по этим вопросам см. в следующих ресурсах.

- [Руководство по выражениям анализа данных (DAX)](/dax/)
- [Функции фильтрации (DAX)](/dax/filter-function-dax)
- Схема обучения: [Использование DAX в Power BI Desktop](https://docs.microsoft.com/learn/paths/dax-power-bi/)
- У вас появились вопросы? [Попробуйте задать вопрос в сообществе Power BI.](https://community.powerbi.com/)
- У вас есть предложения? [Идеи по улучшению Power BI](https://ideas.powerbi.com)
