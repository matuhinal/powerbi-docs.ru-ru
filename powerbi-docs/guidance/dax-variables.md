---
title: 'DAX: Улучшение формул с помощью переменных'
description: Руководство по использованию переменных в выражениях DAX.
author: peter-myers
ms.reviewer: asaxton
ms.service: powerbi
ms.subservice: powerbi-desktop
ms.topic: conceptual
ms.date: 11/23/2019
ms.author: v-pemyer
ms.openlocfilehash: f352cbbd7c42aa54ae876e73c0ed821eccda59c8
ms.sourcegitcommit: 7aa0136f93f88516f97ddd8031ccac5d07863b92
ms.translationtype: HT
ms.contentlocale: ru-RU
ms.lasthandoff: 05/05/2020
ms.locfileid: "74700715"
---
# <a name="dax-use-variables-to-improve-your-formulas"></a>DAX: Улучшение формул с помощью переменных

При моделировании данных написание и отладка некоторых вычислений DAX может быть непростой задачей. Обычно сложные вычисления требуют написания составных или сложных выражений. В составных выражениях может использоваться множество вложенных функций, а логика выражения может повторяться.

Переменные в формулах DAX помогают эффективно реализовывать сложные вычисления. Переменные дают следующие преимущества:

- [повышение производительности](#improve-performance);
- [повышение удобочитаемости](#improve-readability);
- [упрощение отладки](#simplify-debugging);
- [уменьшение сложности](#reduce-complexity).

В этой статье мы продемонстрируем первые три преимущества на примере меры погодового роста продаж. (Формула для погодового роста продаж имеет следующий вид: разность продаж за период и продаж за тот же период прошлого года, _деленная на_ продажи за тот же период прошлого года.)

Начнем со следующего определения меры.

```dax
Sales YoY Growth % =
DIVIDE(
    ([Sales] - CALCULATE([Sales], PARALLELPERIOD('Date'[Date], -12, MONTH))),
    CALCULATE([Sales], PARALLELPERIOD('Date'[Date], -12, MONTH))
)
```

Эта мера дает правильный результат, но ее можно улучшить.

## <a name="improve-performance"></a>Повышение производительности

Обратите внимание, что в формуле повторяется выражение для вычисления продаж за тот же период прошлого года. Power BI приходится вычислить одно и то же выражение дважды, из-за чего формула неэффективна. Повысить эффективность определения меры можно с помощью переменной.

Ниже представлено улучшенное определение меры. В нем используется выражение, присваивающее результат вычисления продаж за тот же период прошлого года переменной с именем **SalesPriorYear**. Затем эта переменная дважды используется в выражении RETURN.

```dax
Sales YoY Growth % =
VAR SalesPriorYear =
    CALCULATE([Sales], PARALLELPERIOD('Date'[Date], -12, MONTH))
RETURN
    DIVIDE(([Sales] - SalesPriorYear), SalesPriorYear)
```

Мера по-прежнему дает правильный результат, но делает это примерно в два раза быстрее.

## <a name="improve-readability"></a>Повышение удобочитаемости

Обратите внимание на то, как в предыдущем определении меры выбранное имя переменной делает выражение RETURN понятнее. Выражение получилось кратким и не требующим дополнительных пояснений.

## <a name="simplify-debugging"></a>Упрощение отладки

Переменные также могут помочь при отладке формулы. Чтобы протестировать выражение, присвоенное переменной, можно временно изменить выражение RETURN так, чтобы оно выводило саму переменную.

Представленное ниже определение меры возвращает только переменную **SalesPriorYear**. Обратите внимание на то, что изначальное выражение RETURN в нем закомментировано. Такой прием позволяет легко восстановить его после завершения отладки.

```dax
Sales YoY Growth % =
VAR SalesPriorYear =
    CALCULATE([Sales], PARALLELPERIOD('Date'[Date], -12, MONTH))
RETURN
    --DIVIDE(([Sales] - SalesPriorYear), SalesPriorYear)
    SalesPriorYear
```

## <a name="reduce-complexity"></a>Уменьшение сложности

В более ранних версиях DAX переменные не поддерживались. В сложных выражениях, в которых добавлялись новые контексты фильтров, требовалось использовать функцию DAX [EARLIER](/dax/earlier-function-dax) или [EARLIEST](/dax/earliest-function-dax) для ссылки на внешние контексты фильтров. Однако эти функции были трудны для понимания и использования.

Переменные всегда вычисляются вне фильтров, которые применяются в выражении RETURN. По этой причине при использовании переменной в измененном контексте фильтра результат получается таким же, как и при использовании функции EARLIEST. Это позволяет обойтись без функций EARLIER и EARLIEST. Таким образом, вы можете писать менее сложные и более понятные формулы.

Рассмотрим приведенное ниже определение вычисляемого столбца, добавленного в таблицу **Subcategory** (Подкатегория). Оно вычисляет рейтинг для каждой подкатегории продуктов на основе значений в столбце **Subcategory Sales** (Продажи подкатегории).

```dax
Subcategory Sales Rank =
COUNTROWS(
    FILTER(
        Subcategory,
        EARLIER(Subcategory[Subcategory Sales]) < Subcategory[Subcategory Sales]
    )
) + 1
```

Функция EARLIER служит для ссылки на значение столбца **Subcategory Sales**_в контексте текущей строки_.

Определение вычисляемого столбца можно улучшить, использовав переменную вместо функции EARLIER. В переменной **CurrentSubcategorySales** хранится значение столбца **Subcategory Sales**_в контексте текущей строки_. Эта переменная используется в выражении RETURN в измененном контексте фильтра.

```dax
Subcategory Sales Rank =
VAR CurrentSubcategorySales = Subcategory[Subcategory Sales]
RETURN
    COUNTROWS(
        FILTER(
            Subcategory,
            CurrentSubcategorySales < Subcategory[Subcategory Sales]
        )
    ) + 1
```

## <a name="next-steps"></a>Дальнейшие действия

Дополнительные сведения по этим вопросам см. в следующих ресурсах.

- Статья о функции [VAR](/dax/var-dax) в DAX
- У вас появились вопросы? [Попробуйте задать вопрос в сообществе Power BI.](https://community.powerbi.com/)
